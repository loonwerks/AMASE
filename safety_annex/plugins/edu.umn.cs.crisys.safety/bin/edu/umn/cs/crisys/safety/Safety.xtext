grammar edu.umn.cs.crisys.safety.Safety with com.rockwellcollins.atc.agree.Agree

generate safety "http://www.xtext.org/example/safety/Safety"

import "http://aadl.info/AADL/2.0" as aadl2

 
@Override
//AnnexLibrary returns agree::AgreeLibrary:
AnnexLibrary returns AnnexLibrary:
	SafetyLibrary;

@Override
//AnnexSubclause returns agree::AgreeSubclause:
AnnexSubclause returns AnnexSubclause:
	SafetySubclause;

SafetyLibrary:
	{SafetyContractLibrary} contract=SafetyContract;

SafetySubclause:
	{SafetyContractSubclause} contract=SafetyContract;

SafetyContract returns Contract:
	{SafetyContract} (specs+=SpecStatement)*;

@Override
Element returns aadl2::Element:
	SafetyEqStatement
;

@Override 
SpecStatement: {FaultStatement} 'fault' str=STRING ':'  faultDefName=Expr  
			faultDefinitions+=(FaultSubcomponent)* ;
	

FaultSubcomponent: 
	{InputStatement} 'input' ':' in_conn=[aadl2::NamedElement] '->' out_conn=ID';' 
	//{InputStatement} 'input' ':' in_connExpr=Expr '->' out_conn=ID ';' 
	| {OutputStatement} 'output' ':' out_conn=ID '->' nom_conn=Expr';' 
 	| {DurationStatement} 'duration' ':' tc=TemporalConstraint interv=TimeInterval ';'  
 	| {TriggerStatement} 'trigger' ':' cond=TriggerCondition  ('['probability=REAL_LIT ']')? ';'
	| SafetyEqStatement ;

	
// Currently, these are represented as keywords. 
TemporalConstraint:
	{PermanentConstraint} 'permanent' 
	| {TransientConstraint} 'transient'
;

// ' 
TriggerCondition:
	{MustCondition}'must' '{' exprList+=(Expr) ("," exprList+=(Expr))* '}'  
  | {EnablerCondition} 'enabler' '{' exprList+=(Expr) ("," exprList+=(Expr))*  '}'  
;
	

// An eq statement can be an agree eq statement ('eq'...) or 
// an interval or a set of discrete values.
 
 // Add capability of integer range?
SafetyEqStatement:
	{EqValue} 'eq' (lhs+=Arg (',' lhs+=Arg)*) ('=' expr=Expr)? ';'
	| {IntervalEq} 'intervaleq' lhs_int=ID '=' interv=TimeInterval ';'
    | {SetEq} 'seteq' lhs_set=ID '=' '{'l1=INTEGER_LIT (',' list+=INTEGER_LIT)* '}' ';'
;



