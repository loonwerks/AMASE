grammar edu.umn.cs.crisys.safety.Safety with com.rockwellcollins.atc.agree.Agree

generate safety "http://www.xtext.org/example/safety/Safety"

import "http://aadl.info/AADL/2.0" as aadl2

 
@Override
AnnexLibrary returns AnnexLibrary:
	SafetyLibrary;

@Override
AnnexSubclause returns AnnexSubclause:
	SafetySubclause;

SafetyLibrary:
	{SafetyContractLibrary} contract=SafetyContract;

SafetySubclause:
	{SafetyContractSubclause} contract=SafetyContract;

SafetyContract returns Contract:
	{SafetyContract} (specs+=SpecStatement)*;

@Override
Element returns aadl2::Element:
	SafetyEqStatement
	| Expr
;

@Override 
SpecStatement: {FaultStatement} 'fault' (str=STRING)? ':'  faultDefName=Expr  
			faultDefinitions+=(FaultSubcomponent)* ;

	

FaultSubcomponent: 
	{InputStatement} 'input' ':' in_conn=Expr ',' out_conn=Arg';' 
	| {OutputStatement} 'output' ':' out_conn=[aadl2::NamedElement|ID] ',' nom_conn=Expr';' 
 	| {DurationStatement} 'duration' ':' tc=TemporalConstraint interv=Interval ';'  
 	| {TriggerStatement} 'trigger' ':' cond=TriggerCondition  ('['probability=REAL_LIT ']')? ';'
	| SafetyEqStatement ;

	 
TemporalConstraint:
	{PermanentConstraint} 'permanent' 
	| {TransientConstraint} 'transient'
;

TriggerCondition:
	{MustCondition}'must' '{' exprList+=(Expr) ("," exprList+=(Expr))* '}'  
  | {EnablerCondition} 'enabler' '{' exprList+=(Expr) ("," exprList+=(Expr))*  '}'  
;
	
SafetyEqStatement:
	{EqValue} 'eq' (lhs+=Arg (',' lhs+=Arg)*) ('=' expr=Expr)? ';'
	| {IntervalEq} 'intervaleq' lhs_int=ID '=' interv=Interval ';'
    | {SetEq} 'seteq' lhs_set=ID '=' '{'l1=INTEGER_LIT (',' list+=INTEGER_LIT)* '}' ';'
;

Interval:
	=>({ClosedInterval} '[' low=Expr ',' high=Expr ']')
|   =>({OpenLeftInterval} '(' low=Expr ',' high=Expr']')
|   =>({OpenRightInterval} '[' low=Expr ',' high=Expr ')')
|   =>({OpenInterval} '(' low=Expr ',' high=Expr ')') 	
;

