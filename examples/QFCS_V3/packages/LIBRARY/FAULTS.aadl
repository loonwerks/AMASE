package FAULTS
public
annex agree {**	
	
	-- Change boolean to integer 
	-- TRUE == 1
	-- FALSE == 0
	node bool_to_int(b: bool) returns (i: int); 
	let
		i = if (b) then 1 else 0;
	tel; 
	
	-- If historically the case, return true
	-- else false
	node historically(b: bool) returns (h: bool); 
	let
	   h = b -> b or pre(h); 
	tel;
	
	-- Return minimum of two values passed in
	node min(m: real, n: real) returns (r:real);
	let
	   r = if (m < n) then m else n;
	tel;
	
	node fail_to(val_in: real, alt_val: real, trigger: bool) returns (val_out: real); 
	let
	   val_out = if (trigger) then alt_val else val_in;
	tel;
	 
    node inverted_fail(val_in: bool, trigger: bool) returns (val_out:bool);
	let
	  val_out = if trigger then not(val_in) else val_in;
	tel;

	-- Seems like this is ramp up... 
	-- Possible change to (val_out - delta) ?
	node ramp_down(val_in: real, delt: real, val_max: real, trigger: bool) returns (val_out: real);
	let
 	  val_out = val_in -> if (trigger) then min(val_max, pre(val_out + delt)) else val_in; 
	tel;	


	-- Faults to add:
	-- Frozen: val_out = val_in
	-- or possibly val_out = pre(val_out) ? Need init if that's the case
	--
	node frozen(val_in: real,alt_val:real, trigger: bool) returns (val_out: real);
	let
	  val_out = if (trigger) then (alt_val) else (val_in);
	tel;
	
	node aill_out_fail_to_val (val_in: DATA_TYPES::Actuators.impl, alt_val: real, trigger: bool) returns (val_out: DATA_TYPES::Actuators.impl); 
	let
	   val_out = if (trigger) then (val_in{AILL := alt_val}) else val_in;
	tel;
	
    node persistent_aill_fail (val_in: DATA_TYPES::BoolActuators.impl, trigger: bool) returns (val_out: DATA_TYPES::BoolActuators.impl); 
	let
	   val_out = if (trigger) then (val_in{AILL := true}) else val_in;
	tel;
	
	node persistent_val1_fail(val_in: DATA_TYPES.quad_bool, trigger: bool) returns (val_out: DATA_TYPES.quad_bool);
	let 
		val_out = if(trigger) then (val_in{val1 := true}) else val_in;
	tel;
	
	node invert_OSAS_S_120_AILL_fail (act: DATA_TYPES::Actuators.impl, act_ret: DATA_TYPES::Actuators.impl, acts_fail: DATA_TYPES::BoolActuators.impl, trigger: bool) returns (val_out: DATA_TYPES::BoolActuators.impl); 
	var
	 fail: bool;
	let
	  fail = 
    		if(OCCURRED.f(RUN_LENGTH.f(OSAS_S_100.req(act.AILL,act_ret.AILL))>= 5)) then
    			true
    		else 
    			(false -> pre (fail));
	   val_out = if (trigger) then (acts_fail{AILL := not(fail)}) else acts_fail;
	tel;	
	
**};	
	
end FAULTS;